/-
Copyright (c) 2023-2025 by the authors listed in the file AUTHORS and their
institutional affiliations. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adrien Champion
-/

import cvc5



/-! # Library setup: re-exports and helpers -/
namespace Cvc



abbrev Set (Œ± : Type) [Ord Œ±] :=
  Lean.RBMap Œ± Unit compare

namespace Set
variable {Œ± : Type} [Ord Œ±]

def empty : Set Œ± := Lean.RBMap.empty
end Set


export Lean (Rat)



/-- The `ùïÇ`onstant combinator. -/
abbrev ùïÇ (val : Œ±) (_ : Œ≤) : Œ± := val

/-! ## Re-exports from `cvc5` -/

inductive Error : Type u
| internal (msg : String)
| unsupported (msg : String)
| userError (msg : String)
deriving Inhabited

namespace Error

def mapMsg (f : String ‚Üí String) : Error ‚Üí Error
| .internal msg => f msg |> .internal
| .unsupported msg => f msg |> .unsupported
| .userError msg => f msg |> .userError

def append (self : Error) (txt : String) (newline := true) : Error :=
  let txt := if newline then "\n"++txt else txt
  self.mapMsg (¬∑ ++ txt)


def toCvc5 : Error ‚Üí cvc5.Error
| .internal "a value is missing" => .missingValue
| .internal msg => .error msg
| .unsupported msg => .unsupported msg
| .userError msg => .error msg

def ofCvc5 : cvc5.Error ‚Üí Error
| .missingValue => .internal "a value is missing"
| .error msg => .internal s!"{msg}"
| .option msg => .internal s!"option error: {msg}"
| .unsupported msg => .unsupported msg
| .recoverable msg => .internal s!"recoverable: {msg}"

instance : MonadLift (Except cvc5.Error) (Except Error) where
  monadLift
  | .ok res => .ok res
  | .error e => .error (ofCvc5 e)

instance : Coe cvc5.Error Error := ‚ü®ofCvc5‚ü©

protected def toString : Error ‚Üí String
| .internal msg => "internal error: " ++ msg
| .unsupported msg => "unsupported: " ++ msg
| .userError msg => "user error: " ++ msg

instance instToString : ToString Error :=
  ‚ü®Error.toString‚ü©

end Error



/-! ## Helpers -/



structure ArrayMin (n : Nat) (Œ± : Type u) : Type u where
mk' ::
  pref : Array Œ±
  inv : pref.size = n := by rfl
  suff : Array Œ± := #[]
deriving Hashable

namespace ArrayMin

instance [Inhabited Œ±] : Inhabited (ArrayMin n Œ±) where
  default := ‚ü®Array.mkArray n default, by simp, #[]‚ü©

def mk (pref : Array Œ±) (suff : Array Œ± := #[]) : ArrayMin pref.size Œ± :=
  ‚ü®pref, rfl, suff‚ü©

protected def toString [ToString Œ±] (self : ArrayMin n Œ±) : String :=
  if self.suff.isEmpty then
    toString self.pref
  else
    s!"{self.pref}{self.suff}"

instance [ToString Œ±] : ToString (ArrayMin n Œ±) := ‚ü®ArrayMin.toString‚ü©


variable (self : ArrayMin n Œ±)

@[simp]
theorem pref_size : self.pref.size = n :=
  self.inv

abbrev size : Nat := n + self.suff.size

@[simp]
theorem min_le_size : n ‚â§ self.size := by
  simp only [Nat.le_add_right]

def get : (i : Fin self.size) ‚Üí Œ±
| ‚ü®i, h_i‚ü© =>
  if h : i < n then
    self.pref.get ‚ü®i, by simp only [pref_size, h]‚ü©
  else
    let h' : i - n < self.suff.size := by
      simp [size] at h_i
      exact Nat.sub_lt_left_of_lt_add (Nat.le_of_not_lt h) h_i
    self.suff.get ‚ü®i - n, by simp only [pref_size, h']‚ü©

instance instGetElem : GetElem (ArrayMin n Œ±) Nat Œ± (fun arr i => i < arr.size) where
  getElem self i h_i := self.get ‚ü®i, h_i‚ü©

def get? (self : ArrayMin n Œ±) (i : Nat) : Option Œ± :=
  if h : i < self.size
  then self.get ‚ü®i, h‚ü©
  else none

def get! [Inhabited Œ±] (self : ArrayMin n Œ±) (i : Nat) : Œ± :=
  if let some a := self.get? i
  then a
  else panic! s!"illegal index {i} for `ArrayMin {n} _` of size {self.size}"

def getN (i : Nat) (h : i < n := by decide) : Œ± :=
  self.pref.get ‚ü®i, by simp [h]‚ü©

def toArray : Array Œ± := self.pref ++ self.suff

def toList : List Œ± := self.pref.data ++ self.suff.data

def push (a : Œ±) : ArrayMin n Œ± :=
  {self with suff := self.suff.push a }

def drainFirst : ArrayMin n.succ Œ± ‚Üí Œ± √ó ArrayMin n Œ±
| ‚ü®‚ü®fst::pref‚ü©, h_pref', suff‚ü© =>
  (fst, ‚ü®
    ‚ü®pref‚ü©,
    by
      simp at h_pref'
      assumption,
    suff
  ‚ü©)

instance instForIn : ForIn m (ArrayMin n Œ±) Œ± where
  forIn self init f := do
    let mut acc := init
    for a in self.pref do
      match ‚Üê f a acc with
      | .done a => return a
      | .yield a => acc := a
    for a in self.suff do
      match ‚Üê f a acc with
      | .done a => return a
      | .yield a => acc := a
    return acc

structure Frame (n : Nat) (Œ± : Type u) : Type u where
private mk ::
  private pref : Array Œ± := #[]
  private suff : Array Œ± := #[]
deriving Inhabited

namespace Frame
def new (n : Nat) : Frame n Œ± :=
  ‚ü®#[], #[]‚ü©

variable (self : Frame n Œ±)

def push (a : Œ±) : Frame n Œ± :=
  if self.pref.size < n then
    {self with pref := self.pref.push a}
  else
    {self with suff := self.suff.push a}

def finalize [Inhabited Œ±] : ArrayMin n Œ± :=
  if h : self.pref.size = n then
    ‚ü®self.pref, h, self.suff‚ü©
  else
    panic! s!"[ArrayMin.finalize] unexpected prefix of size {self.pref.size}, expected {n}"
end Frame

def newFrame : ArrayMin n Œ± ‚Üí Frame n Œ≤
| _ => Frame.new n

structure Iter (n : Nat) (Œ± : Type u) : Type u where
private mk ::
  val : ArrayMin n Œ±
  pos : Nat

namespace Iter
variable (self : Iter n Œ±)

abbrev isNotDone : Bool :=
  self.pos < self.val.size
abbrev isDone : Bool :=
  ¬¨ self.isNotDone

def next? : Option Œ± √ó Iter n Œ± :=
  if h : self.isNotDone then
    let next := self.val.get ‚ü®
      self.pos,
      by simp [isNotDone] at h ; simp [h]
    ‚ü©
    (next, {self with pos := self.pos.succ})
  else (none, self)
end Iter

def iter : Iter n Œ± :=
  ‚ü®self, 0‚ü©

end ArrayMin
